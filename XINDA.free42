00 { 1102-Byte Prgm }
// 4STK mode default, FUNC does not compile
// LBL A-J/a-e are local lettered (for HP 41 execute 15 options)
// Max label local 19
// Max register number so far 04 (fractions)
// Stats reserve registers 11 to 23 (do not alter)
// Temp working register 24 (if needed)
// Local VARS
// L last X
// R REGS
// S temporary local REGS
// T T
// U temporary local T
// X X
// Y Y
// Z Z

// ========= app menu ==========
// divide by related sections
01 LBL "APP"
// MENU keys GTO for menu change, XEQ for operation
02 "FRAC"
// fractions
03 KEY 1 GTO "FRAC"
04 "INTS"
// integer operations
05 KEY 2 XEQ "INTS"
06 "PDFS"
// beta and probability density functions (integration)
07 KEY 3 XEQ "PDFS"
08 ""
09 KEY 4 XEQ "name"
10 ""
11 KEY 5 XEQ "name"
12 ""
13 KEY 6 XEQ "name"
// UP
14 KEY 7 GTO "APP"
// DOWN
15 KEY 8 GTO "APP"
// EXIT
16 KEY 9 GTO "XITEND"
17 GTO "NTRMENU"

// ========= fraction menu ==========
18 LBL "FRAC"
// MENU keys GTO for menu change, XEQ for operation
19 "÷+→"
// store initial fraction as N/D for 4 operation postfix
20 KEY 1 XEQ 09
21 "÷++"
22 KEY 2 XEQ 10
23 "÷+-"
24 KEY 3 XEQ 11
25 "÷+×"
26 KEY 4 XEQ 12
27 "÷+÷"
28 KEY 5 XEQ 13
29 "→÷+"
// print fraction from N/D
30 KEY 6 XEQ 14
// UP
31 KEY 7 GTO "FRAC"
// DOWN
32 KEY 8 GTO "FRAC"
// EXIT
33 KEY 9 GTO "APP"
34 GTO "NTRMENU"

// ========= integers menu ==========
35 LBL "INTS"
// MENU keys GTO for menu change, XEQ for operation
36 "GCD"
// greatest common divisor
37 KEY 1 XEQ "GCD"
38 "LCM"
// least common multiple
39 KEY 2 XEQ "LCM"
40 ""
41 KEY 3 XEQ "name"
42 ""
43 KEY 4 XEQ "name"
44 ""
45 KEY 5 XEQ "name"
46 ""
47 KEY 6 XEQ "name"
// UP
48 KEY 7 GTO "INTS"
// DOWN
49 KEY 8 GTO "INTS"
// EXIT
50 KEY 9 GTO "APP"
51 GTO "NTRMENU"

// ========= PDFs menu ==========
52 LBL "PDFS"
// MENU keys GTO for menu change, XEQ for operation
53 "BETA"
54 KEY 1 XEQ "BETA"
55 ""
56 KEY 2 XEQ "name"
57 ""
58 KEY 3 XEQ "name"
59 ""
60 KEY 4 XEQ "name"
61 ""
62 KEY 5 XEQ "name"
63 ""
64 KEY 6 XEQ "name"
// UP
65 KEY 7 GTO "PDFS"
// DOWN
66 KEY 8 GTO "PDFS"
// EXIT
67 KEY 9 GTO "APP"
68 GTO "NTRMENU"

// ========= menu utilities ==========
// ========= show menus and wait ==========
69 LBL "NTRMENU"
70 MENU
71 LBL 08
72 STOP
73 GTO 08

// ========= clear menus and exit ==========
74 LBL "XITEND"
75 CLMENU
76 EXITALL
77 RTN

// ========= FRAC ==========
// ========= frac local processsing ==========
// don't use temporay REGS as persists
// display post operation uses all 4 levels
// L register is decimal representation
78 LBL 09
// store denominator
79 STO 00
80 R↓
81 X<>Y
82 ×
83 +
// store numerator
84 STO 01
85 RCL÷ 00
// makes decimal in X
86 GTO 16
87 LBL 10
88 XEQ 15
// add
// sub short cut
89 LBL 18
90 XEQ 19
// numerator addition
91 RCL 03
92 STO+ 01
93 R↓
94 GTO 16
95 LBL 11
96 XEQ 15
// sub
97 RCL 01
98 STO- 01
99 STO- 01
100 R↓
101 GTO 18
102 LBL 12
103 XEQ 15
// mul
// div short cut
104 LBL 17
105 RCL 02
106 STO× 00
107 R↓
108 RCL 03
109 STO× 01
110 R↓
111 GTO 16
112 LBL 13
113 XEQ 15
// div
114 RCL 00
115 STO 24
116 R↓
117 RCL 01
118 STO 00
119 R↓
120 RCL 24
121 STO 01
122 R↓
123 GTO 17
124 LBL 14
// to decimal in T, Z+Y/X
125 RCL 01
126 RCL÷ 00
127 ENTER
128 IP
129 ENTER
130 RCL× 00
131 RCL- 01
132 +/-
133 RCL 00
134 R↑
135 STO ST L
136 R↓
137 RTN
// clone and read new
138 LBL 15
139 RCL 00
140 STO 02
141 R↓
142 RCL 01
143 STO 03
144 R↓
145 GTO 09
// cancel GCD
146 LBL 16
147 RCL 01
// zero numerator?
148 X=0?
149 RTN
150 RCL 00
151 XEQ "GCD"
152 STO÷ 01
153 STO÷ 00
// display
154 XEQ 14
155 RTN
// normalize pair
156 LBL 19
157 RCL 00
158 RCL 02
159 XEQ "LCM"
160 STO 00
161 RTN

// ========= INTS ==========
// ========= GCD ==========
162 LBL "GCD"
// like hidden [FIND]?
163 "[GCD]"
// save a return
164 GTO "XINDA"
165 LBL "[GCD]"
166 MOD
167 LASTX
168 X<>Y
169 X≠0?
170 GTO "[GCD]"
171 +
172 XEQ "DRPXY"
173 GTO "XIT1"

// ========= LCM ==========
174 LBL "LCM"
// like hidden [FIND]?
175 "[LCM]"
176 GTO "XINDA"
177 LBL "[LCM]"
178 XEQ "GCD"
179 RCL "X"
180 X<>Y
181 ÷
182 RCL× "Y"
183 XEQ "DRPXY"
184 GTO "XIT1"

// ========= PDFS and special functions ==========
185 LBL "BETA"
186 "[BETA]"
187 GTO "XINDA"
188 LBL "[BETA]"
189 RCL ST Y
190 GAMMA
191 RCL ST Y
192 GAMMA
193 ×
194 RCL ST Z
195 RCL ST Z
196 +
197 GAMMA
198 ÷
199 XEQ "DRPXY"
200 GTO "XIT1"

// ========= execute indirect alpha (entry point) ==========
// Use RCLT if you need the global T restored
// otherwise it's gone for evaluation
// easiest way of getting a "free" working register
201 LBL "XINDA"
// save stack
202 LSTO "X"
203 R↓
204 LSTO "Y"
205 R↓
206 LSTO "Z"
207 R↓
208 LSTO "T"
// make local temp context for local T
209 LSTO "U"
210 R↓
// save last X
211 LASTX
212 LSTO "L"
213 R↓
// save registers
214 RCL "REGS"
215 LSTO "R"
// local temp context for registers
216 LSTO "S"
217 R↓
// do indirect
218 ASTO ST T
// no need for XEQ
219 GTO IND ST T

// ========= exit points ==========
// return a number of items
// tail call GTO
// perform all the clean up FUNC style
// use DRPX to consume input stack datum
// so FUNC 11 is a DRPX XIT1 kinda XEQ
// ========= zero return datums ==========
220 LBL "XIT0"
221 XEQ 00
222 XEQ 04
223 RTN

// ========= one return datum ==========
224 LBL "XIT1"
225 XEQ 00
226 XEQ 03
227 GTO 05

// ========= two return datums ==========
228 LBL "XIT2"
229 XEQ 00
230 XEQ 02
231 GTO 06

// ========= three return datums ==========
232 LBL "XIT3"
233 XEQ 00
234 XEQ 01
235 GTO 07

// ========= four return datums ==========
236 LBL "XIT4"
237 XEQ "STOT"
238 XEQ 00
239 GTO "LRCLT"

// ========= exit stack repair ==========
240 LBL 04
241 RCL "T"
242 LBL 03
243 RCL "Z"
244 LBL 02
245 RCL "Y"
246 LBL 01
247 RCL "X"
248 RTN

// ========= rotary fixing ==========
249 LBL 05
250 R↓
251 LBL 06
252 R↓
253 LBL 07
254 R↓
255 RTN

// ========= T management ==========
// ========= repair global T ==========
256 LBL "RCLT"
257 RCL "T"
258 R↓
259 RTN

// ========= store local T (U) ==========
// needs a LSTO "U" in the outer context
// so that "U" remains local
260 LBL "STOT"
261 R↑
262 STO "U"
263 R↓
264 RTN

// ========= repair local T (U) ==========
265 LBL "LRCLT"
266 RCL "U"
267 R↓
268 RTN

// ========= restore registers and last X ==========
// restore registers called by XIT0-XIT4.
269 LBL 00
270 RCL "R"
271 STO "REGS"
272 R↓
// restore last X
273 RCL "L"
274 STO ST L
275 R↓
276 RTN

// ========= drop one return value ==========
// T->Z->Y->X->L
// local context
277 LBL "DRPX"
278 LSTO "U"
279 XEQ "STOT"
280 RCL "X"
281 STO "L"
282 R↓
283 RCL "Y"
284 STO "X"
285 R↓
286 RCL "Z"
287 STO "Y"
288 R↓
289 RCL "T"
290 STO "Z"
291 R↓
292 GTO "LRCLT"

// ========= drop two return values ==========
293 LBL "DRPXY"
294 XEQ "DRPX"
295 GTO "DRPX"

// ========= temporary register switching ==========
// imoprt local copy of registers to S
296 LBL "IMPRG"
297 XEQ "EXREG"
298 GTO "EXREG"

// this is it's own inverse, if used twice by the above S
// is initialized with a local copy of REGS
// use this to export registers from function
299 LBL "EXREG"
300 XEQ "STOT"
301 RCL "R"
302 STO "S"
303 R↓
304 RCL "REGS"
305 STO "R"
306 R↓
307 XEQ "LRCLT"
// ========= export registers from S ==========
308 LBL "EXPRG"
309 XEQ "STOT"
310 RCL "S"
311 STO "REGS"
312 R↓
313 GTO "LRCLT"

// ========= utility functions ==========
// ========= test shift was last key ==========
314 LBL "SFT?"
315 FS? 64
316 RTNYES
317 RTNNO

// ========= programatic integration template ==========
// subtract the integral MVAR in integrals
// this makes setting it to zero integrates
// and using a program in solve then also works
// MVAR "∫"
// ..
// RCL- "∫"
// RTN
// PGMINT ""
// STO knowns
// STO "LLIM"
// STO "ULIM"
//? STO "ACC"
// INTEG ""

// ========= programatic solve template ==========
// PGMSLV ""
// STO knowns
// SOLVE ""
