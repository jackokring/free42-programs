00 { 884-Byte Prgm }
// 4STK mode default, FUNC does not compile
// LBL A-J/a-e are local lettered
// Max label local 19
// Max register number so far 04
// Stats reserve registers 11 to 23
// Temp working register 24
// Local VARS
// L last X
// R REGS
// S temporary local REGS
// T T
// U temporary local T
// X X
// Y Y
// Z Z

// ========= app menu ==========
01 LBL "APP"
// MENU keys GTO for menu change, XEQ for operation
02 "FRAC"
03 KEY 1 GTO "FRAC"
04 ""
05 KEY 2 XEQ "INTS"
06 ""
07 KEY 3 XEQ "name"
08 ""
09 KEY 4 XEQ "name"
10 ""
11 KEY 5 XEQ "name"
12 ""
13 KEY 6 XEQ "name"
// UP
14 KEY 7 GTO "APP"
// DOWN
15 KEY 8 GTO "APP"
// EXIT
16 KEY 9 GTO "XITEND"
17 GTO "NTRMENU"

// ========= frac menu ==========
18 LBL "FRAC"
// MENU keys GTO for menu change, XEQ for operation
19 "÷+→"
20 KEY 1 XEQ 09
21 "÷++"
22 KEY 2 XEQ 10
23 "÷+-"
24 KEY 3 XEQ 11
25 "÷+×"
26 KEY 4 XEQ 12
27 "÷+÷"
28 KEY 5 XEQ 13
29 "→÷+"
30 KEY 6 XEQ 14
// UP
31 KEY 7 GTO "FRAC"
// DOWN
32 KEY 8 GTO "FRAC"
// EXIT
33 KEY 9 GTO "APP"
34 GTO "NTRMENU"

// ========= integers menu ==========
35 LBL "INTS"
// MENU keys GTO for menu change, XEQ for operation
36 "GCD"
37 KEY 1 XEQ "GCD"
38 ""
39 KEY 2 XEQ "name"
40 ""
41 KEY 3 XEQ "name"
42 ""
43 KEY 4 XEQ "name"
44 ""
45 KEY 5 XEQ "name"
46 ""
47 KEY 6 XEQ "name"
// UP
48 KEY 7 GTO "INTS"
// DOWN
49 KEY 8 GTO "INTS"
// EXIT
50 KEY 9 GTO "APP"
51 GTO "NTRMENU"

// ========= show menus and wait ==========
52 LBL "NTRMENU"
53 MENU
54 LBL 08
55 STOP
56 GTO 08

// ========= clear menus and exit ==========
57 LBL "XITEND"
58 CLMENU
59 EXITALL
60 RTN

// ========= frac local processsing ==========
// ignore resetting REGS back on exit, and fix last X
61 LBL 09
// store denominator
62 STO 00
63 R↓
64 X<>Y
65 ×
66 +
// store numerator
67 STO 01
68 RCL÷ 00
// makes decimal in X
69 GTO 16
70 LBL 10
71 XEQ 15
// add
// sub short cut
72 LBL 18
73 XEQ 19
// numerator addition
74 RCL 03
75 STO+ 01
76 R↓
77 GTO 16
78 LBL 11
79 XEQ 15
// sub
80 RCL 01
81 STO- 01
82 STO- 01
83 R↓
84 GTO 18
85 LBL 12
86 XEQ 15
// mul
// div short cut
87 LBL 17
88 RCL 02
89 STO× 00
90 R↓
91 RCL 03
92 STO× 01
93 R↓
94 GTO 16
95 LBL 13
96 XEQ 15
// div
97 RCL 00
98 STO 24
99 R↓
100 RCL 01
101 STO 00
102 R↓
103 RCL 24
104 STO 01
105 R↓
106 GTO 17
107 LBL 14
// to decimal in T, Z+Y/X
108 RCL 01
109 RCL÷ 00
110 ENTER
111 IP
112 ENTER
113 RCL× 00
114 RCL- 01
115 +/-
116 RCL 00
117 R↑
118 STO ST L
119 R↓
120 RTN
// clone and read new
121 LBL 15
122 RCL 00
123 STO 02
124 R↓
125 RCL 01
126 STO 03
127 R↓
128 GTO 09
// cancel GCD
129 LBL 16
130 RCL 01
// zero numerator?
131 X=0?
132 RTN
133 RCL 00
134 XEQ "GCD"
135 STO÷ 01
136 STO÷ 00
// display
137 XEQ 14
138 RTN
// normalize pair
139 LBL 19
140 RCL 00
141 RCL 02
142 XEQ "GCD"
// temp
143 STO 24
144 RCL 00
145 RCL÷ 24
146 STO× 03
147 RCL 02
148 RCL÷ 24
149 ENTER
150 STO× 01
151 R↓
// and denominator
152 STO× 00
153 RTN

// ========= number theory ==========
// ========= GCD ==========
154 LBL "GCD"
155 ".GCD"
// save a return
156 GTO "XINDA"
157 LBL ".GCD"
158 MOD
159 LASTX
160 X<>Y
161 X≠0?
162 GTO ".GCD"
163 +
164 XEQ "DRPX"
165 XEQ "DRPX"
166 GTO "XIT1"

// ========= execute indirect alpha ==========
// Use RCLT if you need the global T restored
// otherwise it's gone.
167 LBL "XINDA"
// save stack
168 LSTO "X"
169 R↓
170 LSTO "Y"
171 R↓
172 LSTO "Z"
173 R↓
174 LSTO "T"
// make local temp context for local T
175 LSTO "U"
176 R↓
// save last X
177 LASTX
178 LSTO "L"
179 R↓
// save registers
180 RCL "REGS"
181 LSTO "R"
// local temp context for registers
182 LSTO "S"
183 R↓
// do indirect
184 ASTO ST T
// no need for XEQ
185 GTO IND ST T

// ========= exit points ==========
// return a number of items
// perform all the clean up FUNC style
// use DRPX to consume input
// so FUNC 11 is a DRPX XIT1 kinda XEQ
// of a XINDA
// ========= zero return datums ==========
186 LBL "XIT0"
187 XEQ 00
188 XEQ 04
189 RTN

// ========= one return datum ==========
190 LBL "XIT1"
191 XEQ 00
192 XEQ 03
193 GTO 05

// ========= two return datums ==========
194 LBL "XIT2"
195 XEQ 00
196 XEQ 02
197 GTO 06

// ========= three return datums ==========
198 LBL "XIT3"
199 XEQ 00
200 XEQ 01
201 GTO 07

// ========= four return datums ==========
202 LBL "XIT4"
203 XEQ "STOT"
204 XEQ 00
205 GTO "LRCLT"

// ========= exit stack repair ==========
206 LBL 04
207 RCL "T"
208 LBL 03
209 RCL "Z"
210 LBL 02
211 RCL "Y"
212 LBL 01
213 RCL "X"
214 RTN

// ========= rotary fixing ==========
215 LBL 05
216 R↓
217 LBL 06
218 R↓
219 LBL 07
220 R↓
221 RTN

// ========= T management ==========
// ========= repair global T ==========
222 LBL "RCLT"
223 RCL "T"
224 R↓
225 RTN

// ========= store local T (U) ==========
226 LBL "STOT"
227 R↑
228 STO "U"
229 R↓
230 RTN

// ========= repair local T (U) ==========
231 LBL "LRCLT"
232 RCL "U"
233 R↓
234 RTN

// ========= restore registers and last X ==========
// restore registers called by XIT0-XIT4.
235 LBL 00
236 RCL "R"
237 STO "REGS"
238 R↓
// restore last X
239 RCL "L"
240 STO ST L
241 R↓
242 RTN

// ========= drop one return value ==========
// T->Z->Y->X->L
// local context
243 LBL "DRPX"
244 LSTO "U"
245 XEQ "STOT"
246 RCL "X"
247 STO "L"
248 R↓
249 RCL "Y"
250 STO "X"
251 R↓
252 RCL "Z"
253 STO "Y"
254 R↓
255 RCL "T"
256 STO "Z"
257 R↓
258 GTO "LRCLT"

// ========= temporary register switching ==========
// imoprt local copy of registers to S
259 LBL "IMPRG"
260 XEQ "EXREG"
261 GTO "EXREG"

// this is it's own inverse, if used twice by the above S
// is initialized with a local copy of REGS
// use this to export registers from function
262 LBL "EXREG"
263 XEQ "STOT"
264 RCL "R"
265 STO "S"
266 R↓
267 RCL "REGS"
268 STO "R"
269 R↓
270 XEQ "LRCLT"
// ========= export registers from S ==========
271 LBL "EXPRG"
272 XEQ "STOT"
273 RCL "S"
274 STO "REGS"
275 R↓
276 GTO "LRCLT"

// ========= utility functions ==========
// ========= test shift was last key ==========
277 LBL "SFT?"
278 FS? 64
279 RTNYES
280 RTNNO

// ========= programatic integration template ==========
// PGMINT ""
// STO knowns
// STO "LLIM"
// STO "ULIM"
//? STO "ACC"
// INTEG ""

// ========= programatic solve template ==========
// PGMSLV ""
// STO knowns
// SOLVE ""